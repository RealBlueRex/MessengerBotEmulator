plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.0.8'
	id 'org.beryx.jlink' version '2.17.3'
}

java {
	// jdk 14
	sourceCompatibility = JavaVersion.VERSION_14;
	targetCompatibility = JavaVersion.VERSION_14;
}

mainClassName = "$moduleName/org.beuwi.simulator.Launcher"

repositories {
	// jcenter()
	mavenCentral()
}

group 'org.beuwi.simulator'
version '0.5.0'

javafx {
	version = '14'
	modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing', 'javafx.graphics']
}

dependencies {

	implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'

	implementation group: 'org.mozilla', name: 'rhino', version: '1.7.12'

	implementation group: 'commons-io', name: 'commons-io', version: '2.6'

	implementation group: 'com.jfoenix', name: 'jfoenix', version: '9.0.8'

	implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.4'

	// error: module org.beuwi.simulator reads package org.w3c.dom.events from both java.xml and batik.ext
	implementation (group: 'de.codecentric.centerdevice', name: 'javafxsvg', version: '1.3.0') {
		exclude group: 'xalan'
		exclude group: 'xml-apis'
		exclude group: 'org.apache.xmlgraphics', module: 'batik-script'
		exclude group: 'org.apache.xmlgraphics', module: 'batik-ext'
	}

	// error: module org.beuwi.simulator reads package org.hamcrest from both hamcrest.core and junit open
	implementation (group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1') {
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
	}
}

configurations {
}

compileJava {
	options.encoding = "UTF-8"
	options.incremental = true
	options.compilerArgs << "-Xlint:none"

	moduleOptions {
		addExports = [
				// 안하면 package is not visible import 오류남
				'javafx.graphics/com.sun.javafx.util' : 'org.beuwi.simulator',
				'javafx.graphics/com.sun.javafx.iio' : 'javafxsvg'
		]
	}
}

run {
	applicationDefaultJvmArgs = ['--add-exports=javafx.graphics/com.sun.javafx.iio=javafxsvg',
								 '--add-exports=javafx.graphics/com.sun.javafx.iio.common=javafxsvg',
								 '--add-exports=javafx.graphics/com.sun.javafx.scene=javafxsvg',
								 '--add-exports=javafx.graphics/com.sun.glass.ui=javafxsvg',
	]

	jvmArgs = ['--add-exports=javafx.graphics/javafx.scene.layout=org.beuwi.simulator',
			   '--add-exports=javafx.graphics/com.sun.javafx.iio=javafxsvg',
			   '--add-exports=javafx.graphics/com.sun.javafx.iio.common=javafxsvg',
			   '--add-exports=javafx.graphics/com.sun.javafx.scene=javafxsvg',
			   '--add-exports=javafx.graphics/com.sun.glass.ui=javafxsvg',
	]
}

jlink {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	launcher {
		name = 'Messenger Bot Simulator'
	}

	// Instructs the plugin to include all dependencies matching the given prefixes into the merged module. This method is useful when the plugin should handle one or more modular jars as non-modular

	forceMerge 'javafx'

	mergedModule {
		requires 'java.xml'
		requires 'java.desktop'
	}

	jpackage {
		imageOptions += ['--icon', 'src/main/resources/icons/program.ico']

		installerOptions = [
				'--vendor', 'beuwi',
				'--app-version', '0.5.0',
		]
	}
}

jar {
	manifest {
		attributes 'Main-Class': 'org.beuwi.simulator.Launcher'
	}

	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}