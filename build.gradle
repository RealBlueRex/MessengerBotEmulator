plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.0.8'
	id 'org.beryx.jlink' version '2.19.0'
}

java {
	// jdk 14
	sourceCompatibility = JavaVersion.VERSION_14;
	targetCompatibility = JavaVersion.VERSION_14;
}

application {
	mainClassName = "$moduleName/org.hui1601.emulator.Launcher"
}

repositories {
	mavenCentral()
}

group "org.hui1601.emulator"
version '0.9.2'//org.hui1601.emulator.platform.ui.window.IWindowView 179번 줄도 수정

javafx {
	version = '14'
	modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing', 'javafx.graphics']
}

dependencies {

	// implementation group: 'org.json', name: 'json', version: '20190722'
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
	// implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

	implementation group: 'org.mozilla', name: 'rhino', version: '1.7.12'

	implementation group: 'commons-io', name: 'commons-io', version: '2.6'

	implementation group: 'com.jfoenix', name: 'jfoenix', version: '9.0.8'

	implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.4'

	// error: module org.hui1601.emulator reads package org.hamcrest from both hamcrest.core and junit open
	implementation (group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1') {
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.compilerArgs << "-Xlint:none"

	moduleOptions {
		addExports = [
				// 안하면 package is not visible import 오류남
				'javafx.graphics/com.sun.javafx.util' : 'org.beuwi.simulator'
		]
	}
}
run {
}

jlink {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	launcher {
		name = 'Emulator'
	}

	// Instructs the plugin to include all dependencies matching the given prefixes into the merged module. This method is useful when the plugin should handle one or more modular jars as non-modular
	// forceMerge 'javafx'

	mergedModule {
		requires 'java.xml'
		requires 'java.desktop'
		requires 'javafx.graphics'
		requires 'javafx.controls'
	}

	jpackage {
		imageOptions += ['--icon', 'src/main/resources/icons/program.ico']

		installerOptions = [
				'--vendor', 'hui1601',
				'--app-version', version,
		]
	}
}
jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}